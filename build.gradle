plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin'      version '0.0.8'
    id 'org.beryx.jlink'               version '2.17.2'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

repositories {
    mavenCentral()
}

mainClassName = 'fxgame/io.fxgame.game2048.AppLauncher'

javafx {
    version = '13'
    modules = [
       'javafx.base',
       'javafx.graphics',
       'javafx.controls'
    ]
}

// set jpackage installerType based on OS
def os = org.gradle.internal.os.OperatingSystem.current()
def jPkgInstallerType = ''
if (os.isLinux()) {
    jPkgInstallerType = 'deb'
} else if (os.isMacOsX()) {
    jPkgInstallerType = 'pkg'
} else if (os.isWindows()) {
    jPkgInstallerType = 'msi'
}

print(jPkgInstallerType)

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageZip = project.file("${buildDir}/distributions/game2048-${javafx.platform.classifier}.zip")

    launcher {
        name = 'fxgame'
    }

    jpackage {
        // can be set with BADASS_JLINK_JPACKAGE_HOME
        // jpackageHome = "/Users/bruno/fxderek/jdk-14.jdk/Contents/Home"
        outputDir = "installer"
        imageName = 'fx2048'
        skipInstaller = false
        installerName = 'fx2048'
        installerType = "${jPkgInstallerType}"
    }
}

task dist {
    dependsOn clean, jlinkZip
    description "Calls clean and then jlinkZip [default]"
}

defaultTasks 'dist'

eclipse {
    classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') && !it.properties.path.contains("junit") }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
                    it.entryAttributes['module'] = 'true'
                }

            }
        }
    }
}
